uniform sampler2D texture;

// Number of frames of persistance (between 2 and 4 is good)
uniform int currentLine;
uniform int height;

uniform int average;
uniform int persistance;
uniform float bottomValue;
uniform float range;

vec4
rainbow(float value)
{
	vec4 rgb = vec4(0., 0., 0., 1.);
    
    if (value > 0.) {
        // b -> c
        rgb.r = 0.;
        rgb.g = 4. * ( value - (0./4.) );
        rgb.b = 1.;
    }
    
	if( value >= .25 ) {
		// c -> g
		rgb.r = 0.;
		rgb.g = 1.;
		rgb.b = 1. - 4. * ( value - (1./4.) );
	}
	
	if( value >= .50 ) {
		// g -> y
		rgb.r = 4. * ( value - (2./4.) );
		rgb.g = 1.;
		rgb.b = 0.;
	}
	
	if( value >= .75 ) {
		// y -> r
		rgb.r = 1.;
		rgb.g = 1. - 4. * ( value - (3./4.) );
		rgb.b = 0.;
	}
	
    if (value >= 1.) {
		rgb.r = 1.;
		rgb.g = 1.;
		rgb.b = 1.;
    }
    
	return rgb;
}

float getValue(vec2 tex)
{
    float value = 0.;
    float inputY = tex.y;
    
    float deltaY = 1. / float(height);
    
    int i = 0;
    for(i = 0; i < average; i++) {
        // For each sample, look into the past i samples
        tex.y = inputY - (deltaY * float(i));
        
        // Wrap-around the height of the texture
        tex.y = mod(tex.y, 1.);
        
        // Retreive the sample
        float temp = texture2D(texture, tex).a;
        
        // Devide by the number of samples and accumulate
        value += temp * (1. / float(average));
    }
    
    return value;
}

void main()
{
    // Convert the normalized height coordinate into pixels
    vec2 tex;
    tex.x = gl_TexCoord[0].x;
    tex.y = gl_TexCoord[0].y;
    
    float value = getValue(tex);
    float zeroCorrected = value - bottomValue;
    float scaled = zeroCorrected / range;

	gl_FragColor   = rainbow(scaled);
    gl_FragColor.a = 1. / float(persistance);
}
